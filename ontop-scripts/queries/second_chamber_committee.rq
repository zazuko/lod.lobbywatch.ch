PREFIX schema: <http://schema.org/>

DELETE {
  GRAPH <https://lod.lobbywatch.ch> {
    ?s <https://lod.lobbywatch.ch/secondChamberCommittee> ?committee_orig.
  }
}
INSERT {
  GRAPH <https://lod.lobbywatch.ch> {
    ?s <https://lod.lobbywatch.ch/secondChamberCommittee> ?committee_lindas.
  }
}
WHERE {
  GRAPH <https://lod.lobbywatch.ch>{
	{?s a <https://paf.link/Committee>;
      schema:memberOf <https://politics.ld.admin.ch/council/N>;
      schema:alternateName ?abb;
      <https://lod.lobbywatch.ch/secondChamberCommittee> ?committee_orig.

  FILTER (LANG(?abb) = "de")
  BIND(STRLANG(REPLACE(STR(?abb),"NR","SR"), "de") AS ?link)
  SERVICE <https://test.lindas.admin.ch/query> {
    GRAPH <https://lindas.admin.ch/fch/curia> {
      ?committee_lindas schema:memberOf <https://politics.ld.admin.ch/council/S>;
      schema:alternateName ?link.
    }
  }} UNION {
    ?s a <https://paf.link/Committee>;
      schema:memberOf <https://politics.ld.admin.ch/council/S>;
      schema:alternateName ?abb;
      <https://lod.lobbywatch.ch/secondChamberCommittee> ?committee_orig.

  FILTER (LANG(?abb) = "de")
  BIND(STRLANG(REPLACE(STR(?abb),"SR","NR"), "de") AS ?link)
  SERVICE <https://test.lindas.admin.ch/query> {
    GRAPH <https://lindas.admin.ch/fch/curia> {
      ?committee_lindas schema:memberOf <https://politics.ld.admin.ch/council/N>;
      schema:alternateName ?link.
    }
  }
  }
  }
}
