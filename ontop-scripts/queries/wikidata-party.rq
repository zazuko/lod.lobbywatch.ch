PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dbo: <http://dbpedia.org/ontology/>

INSERT {
  ?party <http://www.w3.org/2002/07/owl#sameAs> ?wdparty .
}
WHERE {
  ?party a dbo:PoliticalParty .
  ?party schema:url ?url .
  BIND(REPLACE(STR(?url), "^(https?://)(www.)?([^/]*)(/?).*", "$3") AS ?domain)
  FILTER(?domain = ?domain2)

  SERVICE <https://query.wikidata.org/sparql> {
    ?wdparty wdt:P31 wd:Q7278 .
    ?wdparty wdt:P17 wd:Q39 .
    MINUS {
      ?wdparty wdt:P576 ?dissolved .
    }
    ?wdparty wdt:P856 ?url2 .
    BIND(REPLACE(STR(?url2), "^(https?://)(www.)?([^/]*)(/?).*", "$3") AS ?domain2)
  }
}
